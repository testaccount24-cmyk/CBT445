         MACRO
&NAME    CALLS &RTN,&ARGS,&VL,&ID=,&MF=I
.*
.*  CORRECT FOR THE USUAL IBM DEADWOOD IN A SUPPLIED MACRO.
.*  SAVES ONE INSTRUCTION (4 BYTES) AND MULTIPLE VCONS IF ROUTINE
.*  IS CALLED MORE THAN ONCE.
.*
         GBLB  &IHBSWA
         AIF   ('&MF(1)' EQ 'B').BLDLST
&NAME    L     15,=V(&RTN)         ADDRESS OF ROUTINE
         AIF   ('&ARGS' NE '').GOT
         AIF   ('&MF(1)' NE 'I').GOT
         CALL  (15)                NO ARGUMENTS FORM
         AGO   .ISN
.GOT     ANOP
         CALL  (15),&ARGS,&VL,MF=&MF  ARGUMENT AND VL BIT FORM
.ISN     AIF   ('&ID' EQ '').XIT
         BC    0,&ID               IDENTIFIER, IF ANY
.XIT     ANOP
         MEXIT
.BLDLST  ANOP
&IHBSWA  SETB  ('&VL' EQ 'VL')
         IHBOPLST &RTN,&ARGS,&NAME,MF=(E,&MF(2))
         MEXIT
         MEND
