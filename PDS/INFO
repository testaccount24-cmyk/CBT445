INFO     TITLE 'ADD COMMENTS TO THE LISTING'
***********************************************************************
*                                                                     *
*  add character representation of the instructions and flags for     *
*  instruction characteristics to the listings.                       *
*                                                                     *
*  called by FTXTA                                                    *
*                                                                     *
*  parms @ r1:                                                        *
*    +0 - @ Print Area in PASS1                                       *
*    +4 - input instruction                                           *
*    +10- instruction flags                                           *
*                                                                     *
*  Return is to @ r14.                                                *
*                                                                     *
*  New module written to replace TIME                    CFK mar 2000 *
*                                                                     *
***********************************************************************
         AXNTAB                   equates for flags
         OUTLINE                  map output line
INFO     CSECT
         STM   R14,R12,12(R13)    save callers registers
         BASR  R12,0              setup base reg
         USING *,R12              tell assembler
         L     R2,0(R1)           get @ output area
         USING OUTLINE,R2         tell assembler
         MVI   OUTINFO1,C'*'      mark start of comment
         MVC   OUTINFO2,4(R1)     copy first two bytes
         TR    OUTINFO2,TRTAB     make them printable
         MVI   OUTINFO3,C'*'      assume only two bytes
         TM    4(R1),B'11000000'  2 byte instr ?
         BZ    SETFLAGS           branch if yes.
*              4 or 6 byte instruction
         MVC   OUTINFO3,6(R1)     copy next two bytes
         TR    OUTINFO3,TRTAB     make them printable
         MVI   OUTINFO4,C'*'      assume only four bytes
         TM    4(R1),B'11000000'  4 byte instr ?
         BNO   SETFLAGS           branch if yes.
*              6 byte instruction
         MVC   OUTINFO4,8(R1)     copy last two bytes
         TR    OUTINFO4,TRTAB     make them printable
         MVI   OUTINFO5,C'*'      this must be the end.
*
SETFLAGS DS    0H
*
         TM    10(R1),AXNFTRM     terminating (branch) instr. ?
         BZ    NOTTERM            branch if not
         MVI   OUTINFOB,C'b'      else flag a branch instr.
*
NOTTERM  DS    0H
         TM    10(R1),AXNFINT     interruptible instr. ?
         BZ    NOTINTR            branch if not
         MVI   OUTINFOI,C'i'      else flag interruptible
*
NOTINTR  DS    0H
         TM    10(R1),AXNFPRV     privileged instr. ?
         BZ    NOTPRIV            branch if not
         MVI   OUTINFOP,C'p'      else flag privileged
*
NOTPRIV  DS    0H
         TM    10(R1),AXNFSPRV    semi-privileged instr. ?
         BZ    NOTSPRV            branch if not
         MVI   OUTINFOP,C'q'      else flag semi-privileged
*
NOTSPRV  DS    0H
         TM    10(R1),AXNFSCC     CC setting instr. ?
         BZ    NOTCC              branch if not
         MVI   OUTINFOC,C'c'      else flag it sets CC
*
NOTCC    DS    0H
         LM    R14,R12,12(13)     reload callers registers
         BR    R14                and return
*                   constants
TRTAB    DC    256C'.'            translate table
         ORG   TRTAB+C' '
         DC    C' '
         ORG   TRTAB+C'¢'
         DC    C'¢.<(+|&&'
         ORG   TRTAB+C'!'
         DC    C'!$*);¬-/'
         ORG   TRTAB+C','
         DC    C',%_>?'
         ORG   TRTAB+C'`'
         DC    C'`:#@''="'
         ORG   TRTAB+C'a'
         DC    C'abcdefghi'
         ORG   TRTAB+C'j'
         DC    C'jklmnopqr'
         ORG   TRTAB+C's'
         DC    C'stuvwxyz'
         ORG   TRTAB+C'{'
         DC    C'{ABCDEFGHI'
         ORG   TRTAB+C'}'
         DC    C'}JKLMNOPQR'
         ORG   TRTAB+C'\'
         DC    C'\.STUVWXYZ'
         ORG   TRTAB+C'0'
         DC    C'0123456789'
         ORG   ,                           reset origin to max
         PRINT NOGEN
         REGEQU                            register equates.
         END
