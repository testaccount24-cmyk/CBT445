*
*     PROCESS FILE WITH MORE THAN 1 OBJECT DECK IN IT, EG A PTF.
*
*     THE INPUT IS COPIED TO A WORK FILE AN OBJECT MODULE AT A TIME.
*     EACH OBJECT MODULE IS INPUT INTO THE DISASSEMBLER PROGRAM.
*     AFTER DISASSEMBLY, CONTROL IS RETURNED TO SORCEREM FOR THE
*     NEXT OBJECT MODULE TO BE COPIED.
*
SORCEREM CSECT
         SAVE  (14,12)                 SAVE REGS
         BASR  R12,0                   SET UP OUR BASE REG
         USING *,R12                   TELL ASSEMBLER
         LR    R14,R13                 SAVE HSA ADDRESS
         LA    R13,SAVEAREA            GET @ SAVE AREA
         ST    R13,8(0,R14)            FORWARD POINTER
         ST    R14,4(0,R13)            BACKWARD POINTER
*
         LA    R2,0                    INITIALIZE OBJ.MOD COUNT
         SPACE 2
         OPEN  (OBJMODS)
         SPACE 4
NEXTOBJ  OPEN  (WORKDCB,(OUTPUT))
GETREC   GET   OBJMODS
         LR    R0,R1                   COPY @ RECORD
         CLI   0(R1),X'02'             OBJECT RECORD ?
         BNE   NOPUT                   NO - SKIP PUT
         PUT   WORKDCB                 COPY OBJMOD RECORD TI WORKFULE
NOPUT    DS    0H
         CLC   0(4,R1),ENDC            END OF OBJ.ODULE/Q
         BNE   GETREC                  NO - COPY NEXT RECORD
         CLOSE (WORKDCB)
         SPACE
*    DISASSEMBLE OBJECT MODULE
         LINK  EP=DISAS,ID=0
         LA    R2,1(R2)                  INCREMENT OBJ. MOD COUNT
         STH   R2,*-6                   INSERT COUNT IN LINK ID.
         B     NEXTOBJ
         SPACE 5
EOJ      CLOSE (WORKDCB,,OBJMODS)
         L     R13,4(R13)              GET @ CALLER SAVE AREA
         XC    8(4,13),8(13)           CLEAR CALLER'S HSA CHAIN
         RETURN (14,12),,RC=0
         EJECT
SAVEAREA DS    18F                      SAVE AREA
ENDC     DC    X'02',C'END'
OBJMODS  DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=(GL),EODAD=EOJ
         SPACE 5
WORKDCB  DCB   DDNAME=RDDD,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80
         YREGS
         END
