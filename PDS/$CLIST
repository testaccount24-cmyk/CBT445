PROC 1 DSNAME

/* Function to disassemble an object deck or a load module

/* Change the next 2 lines to your DSNs.
SET LLIB =  FDDSTO7.GEN.LOAD      /* DISAS, SORCEREI, DELINKI, etc */
SET CLIB =  CEE.SCEERUN           /* Run time for DELINKI */

CONTROL NOFLUSH ASIS /* MSG LIST SYMLIST CONLIST

ERROR +
  DO
    SET CC = &LASTCC
    ERROR OFF
    WRITE  ****** Error during setup *******
    WRITE  LASTCC= &CC
    CONTROL NOMSG
    FREE DD(RDDD,PRDD,DADD,PUDD,OUT,LINKCNTL,SYSLIB)
    ALLOC DD(SYSIN)    DS(*) REUSE
    ALLOC DD(SYSPRINT) DS(*) REUSE
    EXIT CODE(20)
  END

ISPEXEC VGET ( ZYEAR ZMONTH ZDAY ZTIMEL ZSCREEN)
ALLOCATE FILE(RDDD) SPACE(1 10) TRACK REUSE +
      LRECL(80)  RECFM(F B)
ALLOC F(PRDD&ZSCREEN) SPACE(1 50) TRACK REUSE +
      LRECL(132) RECFM(F B) MOD DELETE DSN(DISAS.TEMP.PRDD&ZSCREEN)
ALLOC F(PRDD) MOD DSN(DISAS.TEMP.PRDD&ZSCREEN)
ALLOC F(DADD&ZSCREEN) SPACE(1 50) TRACK REUSE +
      LRECL(80)  RECFM(F B) MOD DELETE DSN(DISAS.TEMP.DADD&ZSCREEN)
ALLOC F(DADD) MOD DSN(DISAS.TEMP.DADD&ZSCREEN)
ALLOC F(PUDD)    DS(*) REUSE
ALLOC F(OUT)     DS(*) REUSE

/* find if input is an object deck or load module
LISTDSI &DSNAME
IF &SYSRECFM NE U THEN +
  DO
    /*    Establish the environment needed by DISAS    */
    ALLOCATE FILE(SYSPUNCH) DATASET(&DSNAME) SHR REUSE

    /*  invoke DISAS
    ERROR OFF
    CALL '&LLIB(SORCEREM)'
    FREE F(SYSPUNCH)

  END
ELSE +
  DO
    /*    Extract the member name from the DSN         */

    SET LOC1 = &SYSINDEX((,&DSNAME)
    SET RB = &STR())          /* RIGHT BRACKET */
    SET LOC2 = &SYSINDEX(&STR(&RB),&STR(&DSNAME))
    SET MEM = &SUBSTR(&LOC1+1:&LOC2-1,&DSNAME)

    /*    Establish the environment needed by SORCEREI */

    ALLOCATE FILE(SYSLIB) DATASET(&DSNAME) SHR REUSE RELEASE

    ALLOCATE FILE(SYSLIN) SPACE(1 10) TRACK REUSE +
        LRECL(80) RECFM(F B) MOD DELETE DSN(DISAS.TEMP.INPUT)
    ALLOC F(SYSPRINT) DS(*) REUSE
    ALLOC F(LINKCNTL) DUMMY REUSE
    ALLOC F(SYSIN) SPACE(1) TRACK REUSE +
        LRECL(80) RECFM(F B) BLKSIZE(80)

    /*  Place the member command into the sysin file required by   */
    /*      SORCEREI.                                              */

    OPENFILE SYSIN OUTPUT
    SET SYSIN = &STR(MEMBER(&MEM).)
    PUTFILE SYSIN
    CLOSFILE SYSIN

    /*  invoke SORCEREI

    ERROR OFF
    ISPEXEC LIBDEF ISPLLIB DATASET ID('&LLIB' '&CLIB') STACK
    ISPEXEC SELECT CMD(SORCEREI)
    ISPEXEC LIBDEF ISPLLIB

    FREE DD(LINKCNTL,SYSLIB)
    ALLOC F(SYSIN) DS(*) REUSE
  END

FREE DD(RDDD,PRDD,DADD,PUDD,OUT)
/* display the results of the disassembly
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC CONTROL DISPLAY SAVE
/* Use LMINIT to access the dataset
ISPEXEC LMINIT DATAID(DD) DDNAME(PRDD&ZSCREEN)
SET &ICC = &LASTCC
IF &ICC EQ 0 +
THEN +
  DO
    ISPEXEC BROWSE DATAID(&DD)
    SET BCC = &LASTCC
    ISPEXEC LMFREE DATAID(&DD)
  END
ELSE SET BCC = 0
SET HZERRLM = &ZERRLM /* hold error message

ISPEXEC LMINIT DATAID(DD) DDNAME(DADD&ZSCREEN)
SET &ICC = &LASTCC
IF &ICC EQ 0 +
THEN +
  DO
    ISPEXEC BROWSE DATAID(&DD)
    SET BCC = &LASTCC
    ISPEXEC LMFREE DATAID(&DD)
  END
ELSE SET BCC = 0
SET HZERRLM = &ZERRLM /* hold error message

ISPEXEC CONTROL DISPLAY RESTORE
ISPEXEC CONTROL ERRORS CANCEL

IF &ICC NE 0 OR &BCC NE 0 THEN +
  DO
    /* give out PDF's error message
    WRITE EMBRO&ICC&BCC.E &HZERRLM
  END

FREE DD(PRDD&ZSCREEN,DADD&ZSCREEN)
EXIT
