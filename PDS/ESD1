ESD1     TITLE 'ESD1 '
***********************************************************************
*                                                                     *
*  Process ESD type 1 entry, Label Definition                         *
*                                                                     *
*  Called by ESD                                                      *
*                                                                     *
*  Parms @ R1:                                                        *
*    +0 - @ input card                                                *
*    +4 - @ input ESD entry                                           *
*    +8 - ESD ID of entry                                             *
*  @R14 - return address                                              *
*                                                                     *
*  This csect fills a table of entry points in the input module.      *
*  The table is at entry NENT, which is a fullword count of the       *
*  number of entry points followed by the entries.                    *
*                                                                     *
*  History:                                                           *
*    Added check for table overflow                       CFK Jun 1999*
*    Use register equates                                 CFK Dec 2000*
*    Allow for up to 500 entries                          CFK May 2001*
*    Input parms changed for ESD0                         CFK Nov 2001*
*    Map entry table with ENTRYMAP                        CFK Nov 2001*
*    Use standard linkage conventions                     CFK Sep 2003*
*    Tidy up calls to PRINTA.                             CFK Dec 2003*
***********************************************************************
         ESDITEM                     map input ESD entry      CFK*NOV01
         ENTRYMAP                    map entry table          CFK*NOV01
ESD1     CSECT
         ENTER ,                     ENTRY LOGIC              CFK*SEP03
         ENTRY NENT                  ref'ed by PASS4, PASS3
         EXTRN PRINTA                csect                    CFK*SEP03
         EXTRN PA                    print area in PASS1      CFK*SEP03
         L     R1,4(R1)              get @ input entry
         USING ESDITEM,R1            tell assembler           CFK*NOV01
         L     R2,APA                get @ print area         CFK*NOV01
         L     R3,NENT               )
         LA    R3,1(R3)              ) increment nent
         CH    R3,=AL2(MAXENTRY)     table full ?             CFK*JUN99
         BL    OK                    branch if table not full CFK*JUN99
         MVC   ERRMENT,ESDINAME      else copy entry point    CFK*NOV01
         MVC   0(ERRMSGL,R2),ERRMSG  copy message to print area K*JUN99
         L     R1,APA                get @ PRINTA parms       CFK*DEC03
         L     R15,EPRT1             get @ PRINTA csect       CFK*SEP03
         BASR  R14,R15               call PRINTA              CFK*SEP03
         LEAVE ,                     and return to caller     CFK*SEP03
OK       DS    0H                                             CFK*JUN99
         ST    R3,NENT               )
         LA    R2,13                 get table entry length
         MR    R2,R2                 calc offset into table
         A     R3,AENT               calc @ entry table slot
         USING ENTRYMAP,R3           tell assembler           CFK*NOV01
         MVC   ENTNAME,ESDINAME      copy label name          CFK*NOV01
         MVC   ENTESDID,ESDILDSD     copy SD of csect the     CFK*DEC02
*                                    entry is in              CFK*DEC02
         MVC   ENTADR,ESDIADR        copy label address       CFK*NOV01
         LEAVE ,                     return to caller         CFK*SEP03
         DC    C'IMM='
         LTORG ,                                              CFK*MAY01
EPRT1    DC    A(PRINTA)             @ PRINTA csect           CFK*SEP03
APA      DC    A(PA)                 @ print area in PASS1    CFK*SEP03
AENT     DC    A(ENTN-13)            fiddled @ label tables
ERRMSG   DC    C' TWO MANY ENTRY POINTS, IGNORING '           CFK*JUN99
ERRMENT  DC    CL8' '                                         CFK*JUN99
ERRMSGL  EQU   *-ERRMSG                                       CFK*JUN99
SAVEAREA DS    18F                   save area                CFK*SEP03
NENT     DC    F'0'                  entry point, number of labels
ENTN     DS    8CL1                  label name
ENTS     DS    2CL1                  label SD
ENTA     DS    3CL1                  label address
ENTLN    EQU   *-ENTN                length of one table entry FK*JUN99
         DS    500CL(ENTLN)          space for more labels    CFK*MAY01
MAXENTRY EQU   (*-ENTN)/ENTLN        number of slots in table CFK*JUN99
         DS    0F
         YREGS ,                     register equates         CFK*DEC00
         END
