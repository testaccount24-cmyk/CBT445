PUNCH    TITLE 'STANDARDIZED PUNCH WITH DISK OUTPUT       AUG. 14,1967'
***********************************************************************
*                                                                     *
* Standard routine to write card images to DADD and possibly PUDD.    *
*                                                                     *
* Called directly by PASS3 and PRINTP.                                *
* Called via PRINTP by LSYM, PASS4, SPPR and SYM.                     *
*                                                                     *
* Input - @ R1, 80 byte card image.                                   *
*                                                                     *
* History:                                                            *
*   Changed DCB's to recfm FB instead of recfm F          CFK May 1994*
*   Don't put /* at end of output                         CFK Mar 2000*
*   Use register equates                                  CFK Dec 2000*
*   Use standard linkage                                  CFK Dec 2003*
*                                                                     *
***********************************************************************
PUNCH    CSECT
         ENTRY PUDCB,DADCB        used by PASS5 to close these files
         EXTRN COTR               csect
         ENTER ,                  entry logic                 CFK*DEC03
         LR    R2,R1              SAVE DATA ADR
         TM    DADCB+48,X'10'     ARE DATA SETS OPEN?
         BO    OKPU               BRANCH IF YES
         OPEN  (PUDCB,(OUTPUT),DADCB,(OUTPUT))
         TM    DADCB+48,X'10'     WAS DISK OPEN SUCCESSFUL?
         BO    TPDS               BRANCH IF YES
         ABEND 1,DUMP             DUMP IF UNABLE TO OPEN DISK
TPDS     TM    PUDCB+48,X'10'     WAS PUNCH OPEN SUCCESSFUL?
         BO    OKPU               BRANCH IF YES
         ABEND 3,DUMP             DUMP IF UNABLE TO OPEN PUNCH
OKPU     CLI   9(R2),C' '         IS COL 10 BLANK
         MVI   LT,X'00'           flag to punch card
         BNE   MV                 branch if not blank
         CLI   10(R2),C' '        IS COL 11 BLANK
         MVI   LT,X'01'           flag not to punch card if so
         BE    RTN                SKIP CARD IF YES
         LA    R2,1(R2)           NO,MOVE IMAGE LEFT ONE COL
MV       MVC   PUNO(76),0(R2)     MOVE DATA TO PUNCH AREA
         L     R3,PCCT
         LA    R3,1(R3)           INCREMENT CARD COUNT
         ST    R3,PCCT
         CVD   R3,DW              OBTAIN DECIMAL COUNT
         UNPK  PUNO+76(4),DW+5(3) PLACE UNPACKED COUNT IN COL 77-80
         OI    PUNO+79,X'F0'      REPLACE SIGN BY ZONE (F)
         L     R5,PCOTR           get @ control card image in COTR
         CLI   LT,X'00'           LT clear ?
         BE    PUP                punch card if so.
         CLI   2(R5),C'P'         TEST FOR PUNCH INDICATOR
         BNE   WRDA               skip punch if not set.
PUP      PUT   PUDCB,PUNO         PUNCH A CARD
WRDA     PUT   DADCB,PUNO         CARD TO DISK FOR LATER ASM STEP
RTN      DS    0H                 **********                  CFK*DEC03
         LEAVE ,                  * RETURN *                  CFK*DEC03
DW       DC    D'0'               UNPACKED CARD COUNT
PCOTR    DC    A(COTR)            ADR OF CONTROL CARD IMAGE
PCCT     DC    F'0'               PUNCH CARD COUNT
PUNO     DS    20F                PUNCH AREA
SAVEAREA DS    18F                save area                   CFK*DEC03
LT       DC    X'00'              flag byte
ENDC     DC    CL3'END'
         EJECT
PUDCB    DCB   DDNAME=PUDD,DSORG=PS,MACRF=(PM),RECFM=FB,      CFK*MAY94X
               LRECL=80
DADCB    DCB   DDNAME=DADD,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80 *MAY94
         YREGS ,                  register equates            CFK*DEC00
         END

