PROC 1 DSNAME

/* Create skeleton dissassembly from SYM records only

/* Change the next line to your DSN.
SET LLIB =  FDDSTO7.GEN.LOAD      /* DISAS, SORCEREI, DELINKI, etc */

CONTROL NOFLUSH ASIS /* MSG LIST SYMLIST CONLIST

ERROR +
  DO
    SET CC = &LASTCC
    ERROR OFF
    WRITE  ****** Error during setup *******
    WRITE  LASTCC= &CC
    CONTROL NOMSG
    FREE DD(RDDD,PRDD,DADD,PUDD,OUT,LINKCNTL,SYSLIB)
    ALLOC DD(SYSIN)    DS(*) REUSE
    ALLOC DD(SYSPRINT) DS(*) REUSE
    EXIT CODE(20)
  END

ISPEXEC VGET (ZSCREEN)
ALLOCATE FILE(RDDD) SPACE(1 10) TRACK REUSE +
      LRECL(80)  RECFM(F B)
ALLOC F(PRDD&ZSCREEN) SPACE(1 50) TRACK REUSE +
      LRECL(132) RECFM(F B) MOD DELETE DSN(DISAS.TEMP.PRDD&ZSCREEN)
ALLOC F(PRDD) MOD DSN(DISAS.TEMP.PRDD&ZSCREEN)
ALLOC F(DADD&ZSCREEN) SPACE(1 50) TRACK REUSE +
      LRECL(80)  RECFM(F B) MOD DELETE DSN(DISAS.TEMP.DADD&ZSCREEN)
ALLOC F(DADD) MOD DSN(DISAS.TEMP.DADD&ZSCREEN)
ALLOC F(PUDD)    DS(*) REUSE
ALLOC F(OUT)     DS(*) REUSE

/* find if input is an object deck or load module
LISTDSI &DSNAME
IF &SYSRECFM NE U THEN +
  DO
    /*    Establish the environment needed by DISSYM   */
    ALLOCATE FILE(SYSUT1) DATASET(&DSNAME) SHR REUSE
    ERROR OFF
    CALL '&LLIB(DISSYM)'
    FREE F(SYSUT1)

  END
ELSE +
  DO
    /*    Establish the environment needed by DISYM */
    ALLOCATE FILE(SYSLIN) DATASET(&DSNAME) SHR REUSE
    ALLOC F(SYSPUNCH) DUMMY REUSE
    ERROR OFF
    CALL '&LLIB(DISYM)'
    FREE DD(SYSLIN,SYSPUNCH)
  END

FREE DD(RDDD,PRDD,DADD,PUDD,OUT)
/* display the results of the disassembly
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC CONTROL DISPLAY SAVE
/* Use LMINIT to access the dataset
ISPEXEC LMINIT DATAID(DD) DDNAME(PRDD&ZSCREEN)
SET &ICC = &LASTCC
IF &ICC EQ 0 +
THEN +
  DO
    ISPEXEC BROWSE DATAID(&DD)
    SET BCC = &LASTCC
    ISPEXEC LMFREE DATAID(&DD)
  END
ELSE SET BCC = 0
SET HZERRLM = &ZERRLM /* hold error message

ISPEXEC LMINIT DATAID(DD) DDNAME(DADD&ZSCREEN)
SET &ICC = &LASTCC
IF &ICC EQ 0 +
THEN +
  DO
    ISPEXEC BROWSE DATAID(&DD)
    SET BCC = &LASTCC
    ISPEXEC LMFREE DATAID(&DD)
  END
ELSE SET BCC = 0
SET HZERRLM = &ZERRLM /* hold error message

ISPEXEC CONTROL DISPLAY RESTORE
ISPEXEC CONTROL ERRORS CANCEL

IF &ICC NE 0 OR &BCC NE 0 THEN +
  DO
    /* give out PDF's error message
    WRITE EMBRO&ICC&BCC.E &HZERRLM
  END

FREE DD(PRDD&ZSCREEN,DADD&ZSCREEN)
EXIT
