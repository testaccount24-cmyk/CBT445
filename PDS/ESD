ESD      TITLE 'ESD'
***********************************************************************
*                                                                     *
*  Process ESD card from input                                        *
*                                                                     *
*  Called by PASS1                                                    *
*                                                                     *
*  Parms @ r1:                                                        *
*    +0 - @ input card                                                *
*                                                                     *
*  This csect passes input ESD cards to the appropriate handling      *
*  routine.                                                           *
*                                                                     *
*  History:                                                           *
*    pass weak extrns to ESD2, like normal extrns         CFK may 1999*
*    add the input ESD card to the ILLEGAL ESD CARD msg   CFK may 1999*
*    pass Private Code to ESD0 like SD                    CFK jun 1999*
*    use register equates                                 CFK dec 2000*
*    pass ESD ID separately to ESDx                       CFK nov 2001*
*    after invalid ESD entries check other ESD entries    CFK nov 2001*
*    vet ESD data length                                  CFK nov 2001*
*    Pass PRs to ESD2                                     CFK May 2003*
*    Pass CMs to ESD2, not ESD5                           CFK May 2003*
*    Use standard linkage conventions                     CFK Sep 2003*
*    Tidy up calls to PRINTA.                             CFK Dec 2003*
***********************************************************************
         ESDCARD                      map ESD card            CFK*NOV01
         ESDITEM                      map ESD item on card    CFK*NOV01
ESD      CSECT
         ENTER ,                      entry logic             CFK*SEP03
         EXTRN SCNE                   in ESD2 csect
         EXTRN ESD0                   csect
         EXTRN ESD1                   csect
         EXTRN ESD2                   csect
         EXTRN PA                     in PASS1 csect          CFK*SEP03
         L     R7,0(R1)               get @ input card        CFK*SEP03
         USING ESDCARD,R7             tell assembler          CFK*SEP03
         LA    R8,ESDCENT1            get @ first entry       CFK*NOV01
         USING ESDITEM,R8             tell assembler          CFK*NOV01
         TM    ESDCLEN+1,X'0F'        ESD dat len mul of 16 ? CFK*NOV01
         BNZ   BADCARD                no - invalid card       CFK*NOV01
         CLC   ESDCLEN,=H'0'          any data ?              CFK*NOV01
         BNH   BADCARD                no - invalid card       CFK*NOV01
         CLC   ESDCLEN,=H'48'         length valid ?          CFK*NOV01
         BNH   GOODCARD               yes - valid card        CFK*NOV01
BADCARD  DS    0H                                             CFK*NOV01
         L     R1,=A(PA)              get @ print area        CFK*SEP03
         MVC   0(L'ESDC,R1),ESDC      copy error message      CFK*SEP03
         MVC   L'ESDC(80,R1),ESDCARD  copy input card         CFK*SEP03
         L     R15,=V(PRINTA)         get @ PRINTA csect      CFK*SEP03
         BASR  R14,R15                call PRINTA to o/p msg  CFK*OCT03
         B     Z10Z                   and exit                CFK*SEP03
GOODCARD DS    0H                                             CFK*NOV01
         LH    R9,ESDCID              get first ESD ID        CFK*NOV01
         MVC   ESC+2(2),ESDCLEN       copy length of ESD data
         L     R4,ASCNE               @ scne in esd2 csect
         MVC   2(2,R4),ESDCID         copy ESD identifier of 1st item
         CLC   0(4,R4),C01            =x'00000001' ?
         BNE   COM3                   br if not
         MVI   3(R4),X'02'            else set to x'02'
COM3     LM    R3,R5,ESDB             get BXLE parms
COME     CLC   ESDITYPE,0(R3)     SELECT                      CFK*NOV01
         BE    BESD               ESD
         BXLE  R3,R4,COME         SUBROUTINE
ESD3     DS    0H                 error - unknown entry type  CFK*NOV01
         UNPK  ESDIHEX(15),ESDITEM(8)                         CFK*NOV01
         UNPK  ESDIHEX+14(15),ESDITEM+7(8)                    CFK*NOV01
         UNPK  ESDIHEX+28(5),ESDITEM+14(3)                    CFK*NOV01
         TR    ESDIHEX,=C'0123456789ABCDEF'-240    make hex   CFK*NOV01
         MVI   ESDIHEX+L'ESDIHEX,C' ' clear gash byte         CFK*NOV01
         MVC   ESDICHAR,ESDITEM       copy ESD entry          CFK*NOV01
         TR    ESDICHAR,TRTAB         make printable          CFK*NOV01
         L     R1,=A(PA)              get @ print area        CFK*SEP03
         MVC   0(ESDILN,R1),ESDI      copy error message      CFK*SEP03
         L     R15,=V(PRINTA)         get @ PRINTA csect      CFK*SEP03
         BASR  R14,R15                call PRINTA to o/p msg  CFK*OCT03
         B     NEXTITEM               and try next item       CFK*NOV01
*                  call ESDx subroutine
BESD     S     R3,ESDB                calc offset into table
         LA    R3,AESD(R3)            @ ESDx csect vcon
         L     R15,0(R3)              get @ ESDx csect        CFK*OCT03
         ST    R7,ESDXCARD            pass @ card to ESDx     CFK*SEP03
         ST    R8,ESDXITEM            pass @ current entry    CFK*NOV01
         STH   R9,ESDXID              pass ESD ID             CFK*NOV01
         LA    R1,ESDXPRMS            get @ parameters        CFK*NOV01
         BASR  R14,R15            BRANCH TO SUBR              CFK*OCT03
NEXTITEM DS    0H                                             CFK*NOV01
         LA    R8,16(R8)              get @ next ESD item     CFK*NOV01
         LA    R9,1(R9)               increment ESD ID        CFK*NOV01
         L     R3,ESC                 get len of ESD data
         S     R3,SI6                 subtract len of an ESD entry
         ST    R3,ESC                 save length
         BP    COM3                   br if more entries to process
Z10Z     LEAVE ,                      exit                    CFK*SEP03
         DC    C'IMM='
TRTAB    DC    256C'.'                translate table         CFK*NOV01
         ORG   TRTAB+C' '                                     CFK*NOV01
         DC    C' '                                           CFK*NOV01
         ORG   TRTAB+C''                                     CFK*NOV01
         DC    C'.<(+|&&'                                    CFK*NOV01
         ORG   TRTAB+C'!'                                     CFK*NOV01
         DC    C'!$*);Â¬-/'                                    CFK*NOV01
         ORG   TRTAB+C','                                     CFK*NOV01
         DC    C',%_>?'                                       CFK*NOV01
         ORG   TRTAB+C'`'                                     CFK*NOV01
         DC    C'`:#@''="'                                    CFK*NOV01
         ORG   TRTAB+C'a'                                     CFK*NOV01
         DC    C'abcdefghi'                                   CFK*NOV01
         ORG   TRTAB+C'j'                                     CFK*NOV01
         DC    C'jklmnopqr'                                   CFK*NOV01
         ORG   TRTAB+C's'                                     CFK*NOV01
         DC    C'stuvwxyz'                                    CFK*NOV01
         ORG   TRTAB+C'{'                                     CFK*NOV01
         DC    C'{ABCDEFGHI'                                  CFK*NOV01
         ORG   TRTAB+C'}'                                     CFK*NOV01
         DC    C'}JKLMNOPQR'                                  CFK*NOV01
         ORG   TRTAB+C'\'                                     CFK*NOV01
         DC    C'\.STUVWXYZ'                                  CFK*NOV01
         ORG   TRTAB+C'0'                                     CFK*NOV01
         DC    C'0123456789'                                  CFK*NOV01
         ORG   ,                      reset origin to max     CFK*NOV01
ASCNE    DC    A(SCNE)                @ SCNE in ESD2 csect
AESD     DC    A(ESD2)           @ ESD2 csect, for CM entries CFK*MAY03
         DC    A(ESD2)           @ ESD2 csect      ER
         DC    A(ESD2)           @ ESD2 csect      WX         CFK*MAY99
         DC    A(ESD1)           @ ESD1 csect      LD
         DC    A(ESD0)           @ ESD0 csect      SD
         DC    A(ESD0)           @ ESD0 csect      PC         CFK*JUN99
         DC    A(ESD2)           @ ESD2 csect      PR         CFK*MAY03
         DC    A(ESD2)           @ ESD2 csect      ?          CFK*MAY03
         DC    A(ESD2)           @ ESD2 csect      ?
         DC    A(ESD1)           @ ESD1 csect      ?
         DC    A(ESD0)           @ ESD0 csect      ?
ESDB     DC    A(N02)                 BXLE parms, @ list to search
         DC    F'4'                     length of each entry
         DC    A(N00)                   @ last entry
ESD14    DC    F'0'
N02      DC    X'05000000'           CM type ESD entry, pass to  ESD2
         DC    X'02000000'           ER, External Reference      ESD2
         DC    X'0A000000'           WX, Weak eXternal ref.      ESD2
         DC    X'01000000'           LD, Label (EXTRN)           ESD1
         DC    X'00000000'           SD, Section (CSECT)         ESD0
         DC    X'04000000'           PC, Private Code            ESD0
         DC    X'06000000'           PR, Pseudo Register ESD2 CFK*MAY03
         DC    X'F5000000'                                       ESD2
         DC    X'F2000000'                                       ESD2
         DC    X'F1000000'                                       ESD1
N00      DC    X'F0000000'                                       ESD0
SI6      DC    F'16'
ESC      DC    F'0'                   length of ESD data
C01      DC    F'1'
ESDC     DC    C'**  ILLEGAL ESD CARD:'
ESDI     DC    C'**  ILLEGAL ESD ITEM:'                       CFK*NOV01
ESDIHEX  DS    CL32                    ESD entry in hex       CFK*NOV01
         DC    C' *'                   start of chars         CFK*NOV01
ESDICHAR DS    CL16                    character dump         CFK*NOV01
         DC    C'*'                    end of chars           CFK*NOV01
ESDILN   EQU   *-ESDI                  length of message      CFK*NOV01
ESDXPRMS DS    0F                      parms for ESDx rtns    CFK*NOV01
ESDXCARD DS    F                         @ input card         CFK*NOV01
ESDXITEM DS    F                         @ ESD item           CFK*NOV01
ESDXID   DS    H                         ESD ID of item       CFK*NOV01
SAVEAREA DS    18F                                            CFK*SEP03
         YREGS ,                       register equates       CFK*DEC00
         END
