CXN      TITLE 'CXN, Convert heX to Number'
***********************************************************************
*                                                                     *
*        Convert 4 byte hex number at 0(r14) to 8 bytes bcd starting  *
*        at the same address. Return is to R14+12.                    *
*        R13 is reloaded from R14+8 where the caller should save it.  *
*                                                                     *
*  Called by PASS4, SI, FTXTA, SAVA, PR, PASS3, ESD0, ARL, CONST,     *
*            ADDR and ART                                             *
*                                                                     *
*  Uses R1, R2, R3, R5 as well as R13, R14.                           *
*                                                                     *
*  history:                                                           *
*    use register equates                                CFK dec 2000 *
*                                                                     *
***********************************************************************
CXN      CSECT
         BALR  R13,0               setup base reg
         USING *,R13               tell assembler
         L     R3,0(R14)           load number to convert
         LA    R5,0(R14)           get @ output area
         LA    R1,8                no of nibbles to convert to bytes
SH       SR    R2,R2               clear r2
         SLDL  R2,4                put next nibble into it
         A     R2,AONE             calc adr of correct table entry
         MVC   0(1,R5),0(R2)       copy 1 byte to output area
         LA    R5,1(R5)            point at next output byte
         BCT   R1,SH               if not finished loop
         L     R13,8(R14)          reload callers base register
         BAL   R14,12(R14)         RETURN
         DC    C'IMM='
AONE     DC    A(HX)               @ lookup table
HX       DC    C'0123456789ABCDEF' hex digit to bcd table
         YREGS ,                   register equates           CFK*DEC00
         END

