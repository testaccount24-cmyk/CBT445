PRTA     TITLE 'STANDARDIZED PRINT WITH ERROR ROUTINE     AUG. 14,1967'
***********************************************************************
*                                                                     *
*  Standard print csect to write to PRDD.                             *
*                                                                     *
*  Input @ R1 - 133 byte area to print                                *
*                                                                     *
*  Called by COUNT, ESD, ESD0, ESD1, ESD2, LSYM, PASS1, PASS4,        *
*  PRINTP, READA, RLD, SPPR, SYM and TXT.                             *
*                                                                     *
*  The input area is cleared to spaces after it is printed.           *
*                                                                     *
* History:                                                            *
*   Add opt to bypass call to PUNCH for this record only. CFK May 1999*
*   Change header to ESA/370.                             CFK Jun 1999*
*   Change header to ESA/390.                             CFK Dec 1999*
*   Change header for INFO output.                        CFK Apr 2000*
*   Changed DCB to RECFM=FBA instead of FA.               CFK Nov 2000*
*   Use register equates.                                 CFK Dec 2000*
*   Don't call PUNCH (now done by PRINTP).                CFK Dec 2000*
*                                                                     *
***********************************************************************
PRINTA   CSECT
         ENTRY LCT                printer line count, ref'ed by PASS4
*                                 and COUNT
         ENTRY PRDCB              used by PASS5 to close this DCB
         STM   R14,R12,12(R13)    **************
         BASR  R12,0              *            *              CFK*DEC03
         USING *,R12              *  STANDARD  *
         ST    R13,SVPR+4         *   S/360    *
         LR    R11,R13            *  LINKAGE   *
         LA    R13,SVPR           *            *
         ST    R13,8(R11)         **************
         LR    R3,R1              LOAD DATA SOURCE ADR        CFK*DEC03
         TM    PRDCB+48,X'10'     IS PRINT DATA SET OPEN?
         BO    OKPR               BRANCH IF YES
         OPEN  (PRDCB,(OUTPUT))   NO, OPEN DATA SET
         TM    PRDCB+48,X'10'     WAS PRINT OPEN SUCCESSFUL?
         BO    PG1                BRANCH IF YES
         ABEND 2,DUMP             DUMP IF UNABLE TO OPEN PRINT
PG1      DS    0H
         PUT   PRDCB,MSG          PRINT HEADER MESSAGE
OKPR     L     R5,LCT             )
         LA    R5,1(R5)           ) INCREMENT LINE COUNT
         ST    R5,LCT             )
         C     R5,FFT             IS THIS LAST LINE OF PAGE
         BL    PRNT               BRANCH IF NO
         LA    R5,1               RE-SET LINE
         ST    R5,LCT             COUNT TO ONE
         L     R5,PCT             INCREMENT
         LA    R5,1(R5)           PAGE COUNT
         ST    R5,PCT             BY ONE
         CVD   R5,DPCT            GET DECIMAL PAGE COUNT
         UNPK  PAGE+4(3),DPCT+5(3)
         CLI   PAGE+4,C'0'        REMOVE
         BNE   NOBLK
         MVI   PAGE+4,C' '        LEADING
         CLI   PAGE+5,C'0'
         BNE   NOBLK              ZEROES
         MVI   PAGE+5,C' '
NOBLK    OI    PAGE+6,C'0'        REMOVE DECIMAL SIGN (ZONE)
         PUT   PRDCB,HEAD         PRINT PAGE HEADING
         MVI   0(R3),C'0'         SKIP LINE AFTER HEADING
PRNT     PUT   PRDCB,0(R3)        PRINT A LINE FROM CALLING PROGRAM
         MVI   0(R3),C' '         CLEAR DATA SOURCE LOCATIONS
         MVC   1(134,R3),0(R3)    IN CALLING PROGRAM
         L     R13,SVPR+4         **********
         LM    R14,R12,12(R13)    * RETURN *
         BR    R14                **********
PRERR    MVC   0(44,R3),PRMSG     *** PRINTER ERROR ROUTINE ***
         MVC   45(87,R3),44(R3)   CLEAR PRINT POS BEYOND 45
         B     OKPR               PRINT ERROR MESSAGE & CONTINUE
LCT      DC    F'0'               PRINTER LINE COUNT
FFT      DC    F'60'              MAX LINE COUNT PER PAGE
PCT      DC    F'0'               PAGE COUNT
DPCT     DC    D'0'               DECIMAL PAGE COUNT
SVPR     DC    18F'0'             PRINT-READ SAVE AREA
PRMSG    DC    CL45' *** PRINTER ERROR *** (CHECK PREVIOUS LINE) '
HEAD     DC    CL48'1   LOC  OBJECT CODE          ADDR1     ADDR2 '
         DC    CL55' STMT          SOURCE STATEMENT   APR 2000' K*APR00
VER      DC    CL19'  FLAGS DUMP FMT   '                      CFK*APR00
PAGE     DC    CL10'PAGE '
MSG      DC    CL132'1 ESA/390 DISASSEMBLER PROCESSING MESSAGES'
         EJECT
PRDCB    DCB   DDNAME=PRDD,DSORG=PS,MACRF=(PM),RECFM=FBA,EROPT=ACC,SYNAX
               D=PRERR,LRECL=132                              CFK*NOV00
         YREGS ,                   register equates           CFK*DEC00
         END

