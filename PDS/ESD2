ESD2     TITLE 'ESD2'
***********************************************************************
*                                                                     *
*  Process ESD type 2, 5 or 6 entries, External Reference, Common     *
*  area or Pseudo register.                                           *
*                                                                     *
*  Called by ESD                                                      *
*                                                                     *
*  Parms @ r1:                                                        *
*    +0 - @ input card                                                *
*    +4 - @ input ESD item                                            *
*    +8 - input ESD ID                                                *
*  @r14 - return address                                              *
*                                                                     *
*  History:                                                           *
*    Use register equates                                CFK Dec 2000 *
*    Allow for up to 500 External references             CFK May 2001 *
*    Input parms adjusted for ESD0                       CFK Nov 2001 *
*    Accept PRs (AKA XDs)                                CFK May 2003 *
*    Use standard linkage conventions                    CFK Sep 2003 *
*    Tidy up calls to PRINTA.                            CFK Dec 2003 *
***********************************************************************
         ESDITEM                map ESD item                  CFK*NOV01
         EXTRNMAP               map EXTRN list                CFK*SEP02
ESD2     CSECT
         ENTER ,                entry logic                   CFK*SEP03
         ENTRY SCNE             updated by ESD csect
         ENTRY NEXT             ref'ed by PASS4, ARL,
         EXTRN PRINTA           csect                         CFK*SEP03
         EXTRN PA               print area in PASS1           CFK*SEP03
         L     R3,NEXT          increment
         LA    R3,1(R3)           extern count
         C     R3,TWED          too many ?
         BL    ARESD            no - ok
         BH    Z10Z             if more than max exit now
         ST    R1,ES2R1         save r1                       CFK*NOV01
         L     R1,ESDP2         get @ print area              CFK*NOV01
         MVC   0(36,R1),ESDM2   msg '**  too many external references'
         L     R15,EPRT2        get @ PRINTA csect            CFK*SEP03
         BASR  R14,R15          call PRINTA                   CFK*SEP03
         L     R1,ES2R1         reload r1                     CFK*NOV01
ARESD    ST    R3,NEXT          save new extern count
         LA    R2,15            multiply extrn count
         MR    R2,R2              by 15
         A     R3,AEXT          calc @ next slot in table
         USING EXTRNMAP,R3      tell assembler                CFK*SEP02
         L     R1,4(R1)         get @ input item              CFK*NOV01
         USING ESDITEM,R1       tell assembler                CFK*NOV01
         MVC   EXTSEQ,SCNE+2    copy extern number
         L     R4,SCNE          )
         LA    R4,1(R4)         ) increment scne
         ST    R4,SCNE          )
         L     R2,ESDP2         get @ print area
         MVC   EXTNAME,ESDINAME copy referenced name          CFK*NOV01
         MVC   EXTCMLEN,ESDILEN copy CM or PR length          CFK*MAY03
         MVC   EXTTYPE,ESDITYPE copy ESD type (02, 05 or 06)  CFK*NOV01
         MVC   EXTINFO,ESDIINFO copy PR aligment info         CFK*MAY03
Z10Z     DS    0H                                             CFK*SEP03
         LEAVE ,                return to caller              CFK*SEP03
         DC    C'IMM='
         LTORG ,                                              CFK*MAY01
TWED     DC    A(EXTNMAX)       maximum number of entries     CFK*MAY01
ESDM2    DC    CL36'**  TOO MANY EXTERNAL REFERENCES '        CFK*MAY01
EPRT2    DC    A(PRINTA)        @ PRINTA csect                CFK*SEP03
ESDP2    DC    A(PA)            @ print area                  CFK*SEP03
AEXT     DC    A(EXTN-15)
SCNE     DC    F'0'             entry point, extern number
ES2R1    DC    F'0'             R1 saved here                 CFK*NOV01
SAVEAREA DS    18F              save area                     CFK*SEP03
NEXT     DC    F'0'             entry point, number of EXTRNs read
EXTN     DS    8CL1             referenced name
EXTS     DS    2CL1             reference sequence number
         DS    2CL1             1 info byte, then ESD type, ER or CM
         DS    3CL1             CM (common area) length
         DS    500CL(EXTENTLN)  space for more entries        CFK*MAY01
EXTNMAX  EQU   (*-EXTN)/EXTENTLN maximum number of entries    CFK*MAY01
         DS    0F
         YREGS ,                register equates              CFK*DEC00
         END
