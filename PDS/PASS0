PASS0    TITLE 'PASS 0, call other passes'
***********************************************************************
*                                                                     *
*  This closes files and returns control to the system.               *
*                                                                     *
*  History:                                                           *
*    Set return code to zero                             CFK Jan 2000 *
*    FREEPOOL all DCBs                                   CFK Mar 2000 *
*    Use register equates                                CFK Dec 2000 *
*    Free TXT buffers getmained by ESD0                  CFK May 2001 *
*    Convert to standard linkage conventions             CFK Sep 2003 *
*    Handle concatenated input object decks              CFK Dec 2003 *
*                                                                     *
***********************************************************************
PASS0    CSECT
         EXTRN PRDCB,RDDCB,PUDCB,DADCB
         EXTRN NPROG,NENT,NEXT,NBR                            CFK*DEC03
         ENTRY EXIT               reffed by READA csect
         ENTER ,                  entry logic                 CFK*SEP03
Z10A     DS    0H                                             CFK*DEC03
         CALLS PASS1              call PASS 1                 CFK*SEP03
         LTR   R15,R15            is it EOF ?                 CFK*JAN04
         BNZ   EXIT               exit if so                  CFK*JAN04
         CALLS PASS4              call PASS 4 to do 2nd pass  CFK*SEP03
         CALLS PASS3              call PASS3                  CFK*SEP03
         CALLS PASS4              call PASS4 to do 4th pass   CFK*SEP03
         CALLS COUNT              call COUNT                  CFK*SEP03
*
         FREEMAIN RU,SP=1         free TXT and RLD buffers    CFK*DEC03
*
         LM    R2,R5,=A(NPROG,NENT,NEXT,NBR)  get @ counts    CFK*DEC03
         XR    R0,R0                                          CFK*DEC03
         ST    R0,0(R2)           zero number of csects       CFK*DEC03
         ST    R0,0(R3)           zero number of ENTRYs       CFK*DEC03
         ST    R0,0(R4)           zero number of EXTRNs       CFK*DEC03
         XC    0(12,R5),0(R5)     zero RLD counts             CFK*DEC03
         B     Z10A               and handle next object deck CFK*DEC03
EXIT     DS    0H                                             CFK*JAN04
         CLOSE (PRDCB,,RDDCB,,PUDCB,,DADCB)    close files
         L     R1,=A(PRDCB)                                   CFK*MAR00
         FREEPOOL (1)                          free buffers   CFK*MAR00
         L     R1,=A(RDDCB)                                   CFK*MAR00
         FREEPOOL (1)                          free buffers   CFK*MAR00
         L     R1,=A(PUDCB)                                   CFK*MAR00
         FREEPOOL (1)                          free buffers   CFK*MAR00
         L     R1,=A(DADCB)                                   CFK*MAR00
         FREEPOOL (1)                          free buffers   CFK*MAR00
*
         FREEMAIN RU,SP=2         free SAVA buffer            CFK*DEC03
*
         LEAVE ,                  exit logic                  CFK*SEP03
         LTORG ,                  literal pool                CFK*SEP03
SAVEAREA DC    18F'0'             save area                   CFK*SEP03
         YREGS ,                                              CFK*DEC00
         END
