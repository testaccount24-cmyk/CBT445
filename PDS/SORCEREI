         TITLE 'SORCERER - P.J.CHISLETT 31/10/72'
* THIS PROGRAM PRODUCED BY BRITISH STEEL CORPORATION CORBY
* REFER ANY PROBLEMS TO MALCOLM SISMEY TEL 053-66-2121 EXT 729
*
* MODIFIED BY C.KENDON TO CALL DELINKI AND READ SYSLIN INSTEAD
* OF CALLING DELINK0 AND READING SYSPUNCH
*
*     SORCERER CALLS DELINKI TO PRODUCE A FILE OF OBJECT MODULES.
*     THIS FILE IS COPIED TO A WORK FILE AN OBJECT MODULE AT A TIME.
*     EACH OBJECT MODULE IS INPUT INTO THE DISASSEMBLER PROGRAM.
*     AFTER DISASSEMBLY, CONTROL IS RETURNED TO SORCERER FOR THE
*     NEXT OBJECT MODULE TO BE COPIED.
*
* USE STANDARD ENTRY/EXIT MACROS                              CFK*DEC00
         SPACE 5
SORCEREI CSECT
         SAVE  (14,12)                 SAVE REGS
         BASR  R12,0                   SET UP OUR BASE REG
         USING *,R12                   TELL ASSEMBLER
         LR    R14,R13                 SAVE HSA ADDRESS
         LA    R13,SAVEAREA            GET @ SAVE AREA
         ST    R13,8(0,R14)            FORWARD POINTER
         ST    R14,4(0,R13)            BACKWARD POINTER
*
         LINK  EPLOC=DELINKI           LINK TO DELINKI
         SPACE 2
         OPEN  (OBJMODS)
         SPACE 4
NEXTOBJ  OPEN  (WORKDCB,(OUTPUT))
         LA    R2,0                    INITIALIZE OBJ.MOD COUNT
GETREC   GET   OBJMODS
         LR    R0,R1
         PUT   WORKDCB                 COPY OBJMOD RECORD TI WORKFULE
         CLC   0(4,R1),ENDC            END OF OBJ.ODULE/Q
         BNE   GETREC                  NO - COPY NEXT RECORD
         CLOSE (WORKDCB)
         SPACE 4
*    DISASSEMBLE OBJECT MODULE
         LINK  EP=DISAS,ID=0
         LA    R2,1(R2)                INCREMENT OBJ. MOD COUNT
         STH   R2,*-6                  INSERT COUNT IN LINK ID.
         B     NEXTOBJ
         SPACE 5
EOJ      CLOSE (WORKDCB,,OBJMODS)
         L     R13,4(R13)              GET @ CALLER SAVE AREA
         XC    8(4,13),8(13)           CLEAR CALLER'S HSA CHAIN
         RETURN (14,12),,RC=0
         EJECT
SAVEAREA DS    18F                     SAVE AREA
ENDC     DC    X'02',C'END'
DELINKI  DC    CL8'DELINKI'
OBJMODS  DCB   DDNAME=SYSLIN,DSORG=PS,MACRF=(GL),EODAD=EOJ
         SPACE 5
WORKDCB  DCB   DDNAME=RDDD,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80
         YREGS
         END
