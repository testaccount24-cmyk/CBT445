USING    TITLE 'USING '
***********************************************************************
*                                                                     *
*        Keep track of what USINGs are active at any time             *
*                                                                     *
*  Called by PASS4, ADDR, SPPR and RR                                 *
*                                                                     *
*  Input addressed by R1:                                             *
*    +0  hex flag, 00 - drop, 01 or 02 - insert, 03 - retrieve        *
*    +2  1 byte address part of instruction                           *
*    +3  1 byte address part of instruction                           *
*    +4  3 byte current loc (?) - if insert                           *
*    +7  1 byte instruction format from AXN table                     *
*                                                                     *
*  Output addressed by R1:                                            *
*    +0  1 byte from IC                                               *
*    +1  3 bytes from IC (loc of using?)                              *
*                                                                     *
*  History:                                                           *
*    Use register equates                                CFK Dec 2000 *
*    Use standard linkage conventions                    CFK Sep 2003 *
*                                                                     *
***********************************************************************
USING    CSECT
         ENTER ,                  entry logic                 CFK*SEP03
         L     R3,0(R1)           get input                   CFK*SEP03
         SLL   R3,16              ) mask out
         SRL   R3,28              ) reg no.
         SLL   R3,3               mul by 8
         A     R3,AUS             calc table loc
         C     R3,AUS             IS REGISTER ZERO ?
         BE    RETR2              yes - can't be a base reg
         TM    0(R1),X'03'        NO - what sort of call ?    CFK*SEP03
         BZ    DROP               drop call
         BO    RETR               retrieve
         MVC   1(4,R3),4(R1)      STORE IC IN TABLE           CFK*SEP03
         MVI   0(R3),X'01'        flag this entry in use
         B     RETR               and exit
DROP     MVC   0(8,R3),DUZ        DROP REGISTER by clearing entry
RETR     MVC   1(3,R1),1(R3)      pass back location of using CFK*SEP03
         MVC   0(1,R1),4(R3)      pass back inst format       CFK*SEP03
RETR3    DS    0H                                             CFK*SEP03
         LEAVE                    RETURN                      CFK*SEP03
RETR2    MVC   0(4,R1),DUZ        REGISTER IS ZERO            CFK*SEP03
         B     RETR3              go to return
         DC    C'IMM='
DUZ      DC    D'0'               constant zero
AUS      DC    A(USTB)
USTB     DC    16D'0'             table of usings
* each entry is as follows
*   1 byte flag, x'01' if using active for this register, else x'00'
*   3 bytes instruction counter+2 when using set up
*   1 byte instruction format from AXN table
*   3 bytes padding.
SAVEAREA DS    18F                                            CFK*SEP03
         YREGS ,                  register equates            CFK*DEC00
         END

