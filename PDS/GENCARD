GENCARD  TITLE 'OUTPUT 80-BYTE FIXED-LENGTH RECORD CONTAINING THE JCL PX
               ARM'
***********************************************************************
*                                                                     *
*  THIS PROGRAM ACCEPTS AS INPUT A 1-80 BYTE STRING OF TEXT (IN THE   *
*  FORMAT OF JCL PARM - HALFWORD TEXT LENGTH FOLLOWED BY THE TEXT     *
*  AND OUTPUTS THE DATA AS A FIXED-LENGTH 80-BYTE RECORD TO DDNAME    *
*  SYSPUNCH.  THE DATA IS LEFT-JUSTIFIED IN A FIELD OF BLANKS.        *
*                                                                     *
*  THIS PROGRAM EFFECTIVELY ALLOWS THE SUBSTITUTION OF JCL VARIABLES  *
*  INTO PARAMETER STATEMENTS BEING READ BY PROGRAMS.                  *
*                                                                     *
*  THE DCB ATTRIBUTES OF SYSPUNCH ARE AS FOLLOWS:                     *
*                                                                     *
*                   RECFM ---------------------- F                    *
*                   BLKSIZE -------------------- 80                   *
*                                                                     *
*     PARAMETERS:   1. @ PARM STRING                                  *
*                                                                     *
*     RETURN CODE:  THE RETURN CODE IS ALWAYS ZERO.                   *
*                                                                     *
*     ATTRIBUTES:   RMODE=24                                          *
*                   AMODE=24                                          *
*                                                                     *
*     ABENDS:       U0003 - PARM NOT 1-80 CHARACTERS.                 *
*                   U0013 - OPEN FOR SYSPUNCH FAILED.                 *
*                                                                     *
***********************************************************************
         EJECT
GENCARD  CSECT
         ENTER
*
Z01A     EQU   *
         L     R2,0(,R1)               @ PARM FIELD
         SLR   R3,R3                   PREPARE R3 FOR LENGTH
         ICM   R3,B'0011',0(R2)        LENGTH -> R3
         BZ    ZABEND3                 )
         C     R3,=F'80'               )  ABEND U0003 IF LENGTH INVALID
         BH    ZABEND3                 )
*
Z01B     EQU   *
         OPEN  (SYSPUNCH,(OUTPUT))     OPEN SYSPUNCH
         TM    SYSPUNCH+48,X'10'       )  ABEND U0013 IF
         BZ    ZABEND13                ) "OPEN" FAILED
*
Z01C     EQU   *
         BCTR  R3,0
         EX    R3,MVC1                 BUILD CARD IMAGE
         PUT   SYSPUNCH,CARD           OUTPUT CARD TO SYSPUNCH
*
Z01D     EQU   *
         CLOSE SYSPUNCH                CLOSE SYSPUNCH
*
ZLEAVE   EQU   *
         LEAVE ,                       RETURN TO CALLER
*
*
MVC1     MVC   CARD(0),2(R2)
*
*
ZABEND3  EQU   *                       )
         LA    R1,3                    )
         B     ZABEND                  )
ZABEND13 EQU   *                       )  ERROR ROUTINES
         LA    R1,13                   )
ZABEND   EQU   *                       )
         ABEND (1)                     )
*
*
         PRINT NOGEN
*
*
CARD     DC    CL80' '
*
SAVEAREA DS    18F                                            CFK*JUN04
*
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM,                      X
               RECFM=F,BLKSIZE=80
*
*
         REGEQU
*
*
         END
