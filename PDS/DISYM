         TITLE 'DISYM - Dissassemble SYM records from a LMOD'
***********************************************************************
*                                                                     *
*  Dissassemble the SYM records in a load module.                     *
*                                                                     *
*  This module reads the SYM records from a load module, extracts     *
*  the object SYM cards, passes them to LSYM to generate a skeleton   *
*  dissassembly and puts them to syspunch for later use.              *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*  Dsect to map a SYM record in a load module                         *
*                                                                     *
***********************************************************************
SYML     DSECT
SYMLID   DS    X                       identification X'40'
SYMLIDOK EQU   B'01000000'               SYM record
SYMLTYPE DS    X                       type
SYMLTNO  EQU   B'10000000'               from csect not asm with TEST
SYMLLEN  DS    XL2                     length of SYM data (max 240)
SYMLLEN1 EQU   SYMLLEN+1,1              short length
SYMLCARD DS    XL80                    1st card
SYMLCRD2 DS    XL80                    2nd card
SYMLCRD3 DS    XL80                    3rd card
*
DISYM    CSECT
         SAVE  (14,12)                 SAVE REGS
         BASR  R12,0                   SET UP OUR BASE REG
         USING *,R12                   TELL ASSEMBLER
         LR    R14,R13                 SAVE HSA ADDRESS
         LA    R13,SAVEAREA            GET @ SAVE AREA
         ST    R13,8(0,R14)            FORWARD POINTER
         ST    R14,4(0,R13)            BACKWARD POINTER
*
         OPEN  (INDCB)                 open input DCB
*
         OPEN  (OUTDCB,(OUTPUT))       open output DCB
*
GETREC   DS    0H
*
         GET   INDCB                   get a record
*
         LR    R3,R1                   copy @ lmod record
         USING SYML,R3                 tell assembler
         CLI   SYMLID,SYMLIDOK         SYM record ?
         BNE   EOJ                     no - exit
         CLI   SYMLLEN1,80             long enough for 1 card
         BL    GETREC                  no - get another record
         CLC   SYMC,SYMLCARD           is it a SYM card ?
         BNE   CHK2                    no, try next (probably ESD card)
         LA    R1,SYMLCARD             get @ first card
         L     R15,=V(LSYM)            get @ processing routine
         BASR  R14,R15                 call SYM
         LA    R0,SYMLCARD             get @ first card for put
*
         PUT   OUTDCB                  copy object record to workfile
*
CHK2     DS    0H
*
         CLI   SYMLLEN1,160            long enough for 2nd card
         BL    GETREC                  no - get another record
         CLC   SYMC,SYMLCRD2           is it a SYM card ?
         BNE   CHK3                    no,try next (probably ESD card)
         LA    R1,SYMLCRD2             get @ 2nd card
         L     R15,=V(LSYM)            get @ processing routine
         BASR  R14,R15                 call SYM
         LA    R0,SYMLCRD2             get @ 2nd card for put
*
         PUT   OUTDCB                  copy object record to workfile
*
CHK3     DS    0H
*
         CLI   SYMLLEN1,240            long enough for 3rd card
         BL    GETREC                  no - get another record
         CLC   SYMC,SYMLCRD3           is it a SYM card ?
         BNE   GETREC                  no, get next (probably ESD card)
         LA    R1,SYMLCRD3             get @ 3rd card
         L     R15,=V(LSYM)            get @ processing routine
         BASR  R14,R15                 call SYM
         LA    R0,SYMLCRD3             get @ 3rd card for put
*
         PUT   OUTDCB                  copy object record to workfile
*
         B     GETREC                  get another record
*
EOJ      CLOSE (OUTDCB,,INDCB)
         L     R13,4(R13)              GET @ CALLER SAVE AREA
         XC    8(4,13),8(13)           CLEAR CALLER'S HSA CHAIN
         RETURN (14,12),,RC=0
         EJECT
SAVEAREA DS    18F                     SAVE AREA
SYMC     DS    0CL4                    SYM card header
         DC    X'02',C'SYM'
INDCB    DCB   DDNAME=SYSLIN,DSORG=PS,RECFM=U,MACRF=(GL),EODAD=EOJ
         SPACE 5
OUTDCB   DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80
         YREGS
         END
