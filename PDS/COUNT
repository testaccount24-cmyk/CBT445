COUNT    TITLE '*** C O U N T ***  (WITH PAGE HEADING)    AUG. 14,1967'
***********************************************************************
*                                                                     *
*   Output instruction usage table at end of listing                  *
*                                                                     *
*  Called by PASS0                                                    *
*                                                                     *
* History:                                                            *
*   Disable punching when printing table                  CFK May 1999*
*   Move count table into AXN                             CFK May 1999*
*   Use register equates                                  CFK Dec 2000*
*   Use standard linkage conventions                      CFK Sep 2003*
*   Tidy up calls to PRINTA.                              CFK Dec 2003*
*   Mnemonic is 5 bytes.                                  CFK Jan 2004*
*   Concatenated object deck support:                     CFK Jan 2004*
*     Ensure new page started at start of every call.     CFK Jan 2004*
*                                                                     *
***********************************************************************
         AXNTAB                      Map AXN table            CFK*MAY99
COUNT    CSECT
         EXTRN LCT                   in PRINTA csect
         EXTRN MEN                   @ AXN table
         EXTRN PRINTA                csect                    CFK*SEP03
         EXTRN PA                    in PASS1 csect
         EXTRN PAB                   in PASS1 csect           CFK*OCT03
         ENTER ,                     entry logic              CFK*SEP03
         L     R8,PAA                get @ print o/p buffer in PASS1
         MVI   0(R8),C' '            clear
         MVC   1(131,R8),0(R8)        print buffer
         L     R2,AAMEN              get @ opcode name table in AXN
         USING AXNTAB,R2             tell assembler
         SR    R9,R9                 clear total use this pass
         L     R10,TOTAL             get grand total
CCLL     SR    R6,R6
         L     R5,AXNTOTCT           ) add current
         A     R5,AXNUSECT           )  use count
         ST    R5,AXNTOTCT           )   to total count
         CR    R6,R5                 use count > 0 ?
         BE    CLLA                  no - skip printing
         SRL   R5,1
         CVD   R5,DWM                make total use decimal
         UNPK  FPW(8),DWM(8)           and printable
         OI    FPW+7,C'0'            add sign
         MVC   2(5,R8),AXNAME        copy mnemonic to output  CFK*JAN04
         MVC   28(5,R8),FPW+3        copy total use to output buffer
         L     R5,AXNUSECT           get use count
         AR    R9,R5                 add to total
         AR    R10,R5                add to grand total
         ST    R6,AXNUSECT           zero use count
         SRL   R5,1
         CVD   R5,DWM                make use count decimal
         UNPK  FPW(8),DWM(8)           and printable
         OI    FPW+7,C'0'            add sign
         MVC   13(5,R8),FPW+3        copy to output buffer
         L     R14,CLCT              ) inc
         LA    R14,1(R14)            )  current
         ST    R14,CLCT              )   line count
         C     R14,MCLCT             >= max line count
         BL    GOCT                  br if not
* else force new page and o/p headers
         L     R14,ALCT              get @ printer line count in PRINTA
         L     R7,APAB               get @ 2nd print buffer   CFK*OCT03
         MVC   0(4,R14),CLCT         set to 59
         MVC   CLCT(4),ZRCT          and clear counter
         MVC   48(L'HEAD1,R7),HEAD1  copy header to buffer    CFK*OCT03
         L     R15,APRINC            get @ PRINTA csect       CFK*SEP03
         LR    R1,R7                 get @ print buffer       CFK*DEC03
         BASR  R14,R15               call PRINTA              CFK*SEP03
         MVC   0(L'HEAD2,R7),HEAD2   copy header to buffer    CFK*OCT03
         L     R15,APRINC            get @ PRINTA csect       CFK*SEP03
         LR    R1,R7                 get @ print buffer       CFK*DEC03
         BASR  R14,R15               call PRINTA              CFK*SEP03
GOCT     L     R15,APRINC            get @ PRINTA csect       CFK*SEP03
         LR    R1,R8                 get @ print line         CFK*DEC03
         BASR  R14,R15               call PRINTA              CFK*SEP03
CLLA     LA    R2,AXNNEXT            @ next opcode table entry
         CLI   AXNAME,C' '           end of table ?
         BNE   CCLL                  no, process next opcode
*                       table finished, output totals
         MVC   0(8,R8),TOT         c'0TOTALS'
         SRL   R9,1
         CVD   R9,DWM              make total decimal
         UNPK  FPW(8),DWM(8)       ) and
         OI    FPW+7,C'0'          )  printable
         MVC   13(5,R8),FPW+3      copy to Print Area
         ST    R10,TOTAL           save grand total
         SRL   R10,1
         CVD   R10,DWM             make it decimal
         UNPK  FPW(8),DWM(8)       ) and
         OI    FPW+7,C'0'          )  printable
         MVC   28(5,R8),FPW+3      copy to Print Area
         L     R15,APRINC          get @ PRINTA csect         CFK*SEP03
         MVI   CLCT+3,59           need new page next time    CFK*JAN04
         LR    R1,R8               get @ print area           CFK*DEC03
         BASR  R14,R15             call PRINTA                CFK*SEP03
         LEAVE ,                   and exit
         DC    C'IMM='
* constants                                                   CFK*SEP03
         LTORG                                                CFK*SEP03
PAA      DC    A(PA)               @ print buffer in PASS1    CFK*SEP03
APAB     DC    A(PAB)              @ print buffer in PASS1    CFK*OCT03
MCLCT    DC    F'57'               max lines per page
ZRCT     DC    F'0'                constant zero
AAMEN    DC    A(MEN)              opcode name table in AXN
APRINC   DC    A(PRINTA)           @ print routine            CFK*SEP03
ALCT     DC    A(LCT)              @ LCT in PRINTA csect
TOT      DC    C'0TOTALS '
HEAD1    DC    C'--- INSTRUCTION USAGE TABLE --- '            CFK*OCT03
HEAD2    DC    C'0INSTR   CURRENT USAGE   TOTAL USAGE '       CFK*OCT03
* variables                                                   CFK*SEP03
CLCT     DC    F'59'               current line on page counter
TOTAL    DC    F'0'                grand total
DWM      DC    D'0'                work area for CVD
FPW      DC    D'0'                work area to unpack to
SAVEAREA DS    18F                                            CFK*SEP03
         YREGS ,                   register equates           CFK*DEC00
         END
