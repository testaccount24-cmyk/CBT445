ART      TITLE 'ART '
***********************************************************************
*                                                                     *
*  Called by PASS4 during the 4th pass to fetch the next address to   *
*  put on the listing.                                                *
*                                                                     *
*  Parms @ r1:                                                        *
*    +0 - output type if address, x'FF' if none left                  *
*    +1 - 3 byte output address, x'FFFFFF' if none left.              *
*    +4 - 8 byte label or Annnnn if no label                          *
*                                                                     *
*  History:                                                           *
*    Map address table with LABELMAP                     CFK Apr 2000 *
*    Use register equates                                CFK Sep 2000 *
*    Generate label if necessary                         CFK Sep 2000 *
*    Eliminate call to CXN                               CFK Sep 2001 *
*    Use standard linkage conventions                    CFK Sep 2003 *
*    Rewrite to only return labels in current section.   CFK Dec 2003 *
*    Let PASS4 reset pointers at start of each csect.    CFK Jan 2004 *
*                                                                     *
***********************************************************************
         LABELMAP                 map label table in SAVA     CFK*APR00
*
PRMDUM   DSECT                    dsect to map parms          CFK*SEP00
PRMENTRY DS    0XL12              whole entry to return       CFK*SEP00
PRMFLAG  DS    X                  flag                        CFK*SEP00
PRMADR   DS    XL3                address                     CFK*SEP00
PRMLABEL DS    XL8                label                       CFK*SEP00
*
ART      CSECT
         USING PRMDUM,R1          tell assembler about parms  CFK*SEP00
         ENTER ,                  entry logic                 CFK*SEP03
         ENTRY NEL4               ref'ed by PASS3
         ENTRY ARTCSECT           ref'ed by PASS4             CFK*DEC03
         LM    R3,R4,NEL4         get no. of adrs and @ them  CFK*DEC03
         USING LABELMAP,R4        tell assembler              CFK*APR00
         LTR   R3,R3              any addresses left ?        CFK*DEC03
         BZ    Z10B               br. if none left            CFK*JAN04
Z10A     DS    0H                                             CFK*DEC03
         CLC   LABCSECT,ARTCSECT  is entry for current csect? CFK*DEC03
         BE    Z10C               if so process it            CFK*DEC03
         BH    Z10B               if later csect EOF          CFK*JAN04
         LA    R4,LABENTLN(R4)    else point @ next address   CFK*DEC03
         BCT   R3,Z10A            DECREMENT ADDR. COUNT       CFK*DEC03
Z10B     DS    0H                                             CFK*DEC03
         MVI   PRMFLAG,X'FF'      flag if no addresses left   CFK*DEC03
         MVC   PRMADR,=X'FFFFFF'  set dummy address           CFK*DEC03
         B     Z10E               and exit                    CFK*JAN04
Z10C     DS    0H                                             CFK*DEC03
         MVC   PRMFLAG,LABTYPE    copy type                   CFK*DEC03
         MVC   PRMADR(11),LABADR  copy address and name       CFK*DEC03
         CLI   LABNAME,C' '       is there a label ?          CFK*SEP00
         BNE   Z10D               branch if so                CFK*DEC03
* convert address to label                                    CFK*SEP00
         UNPK  PRMLABEL(7),LABADR(4) unpack address           CFK*SEP01
         NI    PRMLABEL,X'0F'     mask out low nybble         CFK*SEP00
         TR    PRMLABEL(1),TRTAB  make 0-9 into A-P           CFK*SEP00
         TR    PRMLABEL+1(5),=C'0123456789ABCDEF'-240 make ch CFK*SEP01
         MVI   PRMLABEL+6,C' '    clear gash byte             CFK*SEP01
Z10D     DS    0H                                             CFK*JAN04
         LA    R4,LABENTLN(R4)    point @ next address        CFK*DEC03
         BCTR  R3,0               DECREMENT ADDR. COUNT       CFK*DEC03
Z10E     DS    0H                                             CFK*JAN04
         STM   R3,R4,NEL4         save new address and count  CFK*DEC03
         LEAVE ,                  RETURN                      CFK*SEP03
         DC    C'IMM='
*        LTORG ,                                              CFK*SEP03
TRTAB    DC    C'ABCDEFGHIJKLMNOP'  tr into A-P               CFK*SEP01
SAVEAREA DS    18F                                            CFK*SEP03
ARTCSECT DS    CL8                current csect, set by PASS4 CFK*DEC03
NEL4     DC    F'0'               number of addresses, set by PASS3
AST4     DC    F'0'               entry point, @ adr. tab. set by PASS3
NEL4B    DC    F'0'               copy of NEL4, set by PASS3  CFK*DEC03
AST4B    DC    F'0'               copy of AST4, set by PASS3  CFK*DEC03
         YREGS ,                  map registers               CFK*SEP00
         END
