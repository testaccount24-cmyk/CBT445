READ     TITLE 'STAND. READ WITH EOF && RDERR ROUTINES   AUG. 14, 1967'
***********************************************************************
*                                                                     *
* Standard read routine to read the next input card from RDDD.        *
*                                                                     *
* Called by PASS1 and PASS3.                                          *
*                                                                     *
* Input, @ R1                                                         *
*   +0 - @ 80 byte buffer to put the card in                          *
*                                                                     *
* Return is to where R14 points if a card was succesfully read.       *
*                                                                     *
* If an I/O error occurs an error message is issued and the next card *
* is read.                                                            *
*                                                                     *
* History:                                                            *
*   Changed DCB from recfm F to recfm FB.               CFK May 2000  *
*   Use register equates.                               CFK Dec 2000  *
*   Don't output error message at EOF if last card was  CFK Sep 2001  *
*   an END card.                                                      *
*   Set RC 0 if not EOF                                 CFK Oct 2003  *
*   Tidy up calls to PRINTA                             CFK Dec 2003  *
*   Return with RC 8 of EOF, not branch to EXIT         CFK Jan 2004  *
*                                                                     *
***********************************************************************
READA    CSECT
         ENTRY RDDCB              used by PASS5 to close this DCB
         ENTRY ENDFLAG            set by END of EOF valid     CFK*SEP01
         EXTRN EXIT               in PASS0 csect
         EXTRN PA                 in PASS1 csect
         EXTRN PRINTA             csect
         STM   R14,R12,12(R13)    **************
         BALR  R12,0              *            *
         USING *,R12              *  STANDARD  *
         ST    R13,SVRD+4         *   S/360    *
         LR    R11,R13            *  LINKAGE   *
         LA    R13,SVRD           *            *
         ST    R13,8(R11)         **************
         L     R5,0(R1)           LOAD DATA DESTINATION ADR
         TM    RDDCB+48,X'10'     IS READ DATA SET OPEN?
         BO    OKRD               BRANCH IF YES
         OPEN  (RDDCB,(INPUT))    NO, OPEN DATA SET
         TM    RDDCB+48,X'10'     WAS OPEN SUCCESSFUL?
         BO    OKRD               BRANCH IF YES
         ABEND 4,DUMP             DUMP IF UNABLE TO OPEN READ
OKRD     GET   RDDCB,0(R5)        READ A CARD INTO CALLING PROGRAM
         MVI   ENDFLAG,X'00'      flag EOF not valid          CFK*SEP01
         L     R13,SVRD+4         **********
         LM    R14,R12,12(R13)    * RETURN *
         XR    R15,R15            set good RC                 CFK*OCT03
         BR    R14                **********
RDERR    DS    0H                                             CFK*DEC03
         L     R1,APA             ERROR MESSAGE
         MVC   0(40,R1),RDMSG     AND CARD IMAGE
         MVC   40(80,R1),0(R5)    TO PRINT AREA
         MVI   119(R1),C' '       BLANK REMAINING
         MVC   120(11,R1),119(R1) PRINT POSITIONS
         LA    R1,APA             LOAD PRINT PARM
         L     R15,APRN           PRINT ERROR MESSAGE
         LA    R14,OKRD           THEN READ NEXT CARD
         BR    R15                OVER ERROR IMAGE
EOF      DS    0H                 *** END OF FILE ROUTINE *** CFK*SEP01
         CLI   ENDFLAG,X'FF'      is EOF valid ?              CFK*SEP01
         BE    EXITEOF            if so exit                  CFK*JAN04
         L     R1,APA             else get @ PRINT area
         MVC   0(32,R1),EOFMG     EOF MSG TO PRINT AREA
         MVC   33(100,R1),32(R1)  CLEAR PRINT POS BEYOND 32
         MVC   54(31,R1),EOFMG+1  EOF MSG TO CARD IMAGE
         L     R15,APRN           get @ PRINTA csect
         BASR  R14,R15            PRINT EOF MESSAGE, THEN     CFK*JAN04
EXITEOF  DS    0H                                             CFK*JAN04
         L     R13,SVRD+4         **********                  CFK*JAN04
         LM    R14,R12,12(R13)    * RETURN *                  CFK*JAN04
         LA    R15,8              set EOF RC                  CFK*JAN04
         BR    R14                **********                  CFK*JAN04
SVRD     DC    18F'0'             READ TO PRINT SAVE AREA
APA      DC    A(PA)              ADR OF PRINT AREA
APRN     DC    A(PRINTA)          ADR OF STANDARDIZED PRINT ROUTINE
RDMSG    DC    CL40' *** READ ERROR *** THIS CARD IGNORED : '
EOFMG    DC    CL33'0*** END OF FILE TERMINATION *** '
ENDFLAG  DC    X'00'              set to X'FF' if EOF valid   CFK*SEP01
         EJECT
RDDCB    DCB   DDNAME=RDDD,DSORG=PS,MACRF=(GM),RECFM=FB,LRECL=80,      X
               EODAD=EOF,EROPT=ACC,SYNAD=RDERR
         YREGS ,                  register equates            CFK*DEC00
         END
