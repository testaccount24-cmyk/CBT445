PR       TITLE 'PR  '
***********************************************************************
*                                                                     *
*        format object code in listing                                *
*                                                                     *
*        as xx xx             - 2 byte instructions                   *
*           xx xx x xxx       - 4 byte instructions                   *
*           xx xx x xxx x xxx - 6 byte instructions                   *
*                                                                     *
*  called by FTXTA                                                    *
*                                                                     *
*  input: addresed by R1                                              *
*    +0  - @ output area                                              *
*    +4  - instruction to format                                      *
*    +12 - instruction length                                         *
*                                                                     *
*  history:                                                           *
*    map output line with outline macro                  CFK feb 2000 *
*    user register equates                               CFK nov 2000 *
*    map parameters with dsect                           CFK dec 2000 *
*    use standard entry/exit logic + don't use CXN       CFK jun 2001 *
*                                                                     *
***********************************************************************
PRMDUM   DSECT                    parameters                  CFK*DEC00
PRM@PA   DS    A                  @ output area               CFK*DEC00
PRMINST  DS    XL6                2-6 byte inst to format     CFK*DEC00
         DS    XL2                padding                     CFK*DEC00
PRMILEN  DS    XL4                instruction length          CFK*DEC00
*                                                             CFK*DEC00
         OUTLINE                  map output line             CFK*FEB00
PR       CSECT
         ENTER ,                  entry logic                 CFK*JUN01
         USING PRMDUM,R1          tell assembler about parms  CFK*JUN01
         L     R3,PRM@PA          get @ output area
         USING OUTLINE,R3         tell assembler              CFK*FEB00
         UNPK  HEXWORK,PRMINST(7) unpack input + 1 byte       CFK*JUN01
         MVZ   HEXWORK(12),ZEROES clear high nybbles          CFK*JUN01
         TR    HEXWORK(12),=C'0123456789ABCDEF'      make hex CFK*JUN01
         MVC   OUTOBJ1,HEXWORK    output first byte           CFK*JUN01
         MVC   OUTOBJ2,HEXWORK+2  output second byte          CFK*JUN01
         CLI   PRMILEN+3,X'04'    check instruction length    CFK*JUN01
         BL    PRET               if 2 byte instr. exit       CFK*JUN01
         MVC   OUTOBJ3,HEXWORK+4  1st base register           CFK*JUN01
         MVC   OUTOBJ4,HEXWORK+5  1st displacement            CFK*JUN01
         BE    PRET               if 4 byte instr. exit       CFK*JUN01
         MVC   OUTOBJ5,HEXWORK+8  2nd base reg                CFK*JUN01
         MVC   OUTOBJ6,HEXWORK+9  2nd displacement            CFK*JUN01
PRET     LEAVE ,                  return to caller            CFK*JUN01
ZEROES   DC    XL12'00'           binary zeroes               CFK*JUN01
HEXWORK  DS    CL13               work area to unpack to      CFK*JUN01
         YREGS                    register equates            CFK*NOV00
SAVEAREA DS    18F                                            CFK*SEP03
         END

