FXPT     TITLE 'FXPT, Format Fixed Point Constants in output'
***********************************************************************
*                                                                     *
*  Format fixed point constants in output                             *
*                                                                     *
*  Called by CONST                                                    *
*                                                                     *
*  Parms @ r1: (mapped by PRMDUM)                                     *
*    +0 - @ Print Area                                                *
*    +4 - output constant                                             *
*    +8 - callers r13                                                 *
*    +12 - return to here                                             *
*                                                                     *
*  Attributes:                                                        *
*    Reusable                                                         *
*                                                                     *
*  History:                                                           *
*    Use OUTLINE macro to map output line                CFK Feb 2000 *
*    Use register equates                                CFK Dec 2000 *
*    Map input parameters                                CFK Dec 2000 *
*    Use standard linkage conventions                    CFK Nov 2003 *
*                                                                     *
***********************************************************************
PRMDUM   DSECT                                                CFK*DEC00
PRM@PA   DS    A                      @ print area            CFK*DEC00
PRMTXT   DS    XL4                    constant to print       CFK*DEC00
*
         OUTLINE                      map output line         CFK*FEB00
FXPT     CSECT
         ENTER ,                      entry logic             CFK*NOV03
         LR    R10,R1                 get @ parms             CFK*NOV03
         USING PRMDUM,R10             tell assembler          CFK*NOV03
         L     R4,PRM@PA              get @ print area
         USING OUTLINE,R4             tell assembler          CFK*FEB00
         MVC   OUTOPER(2),FQ          copy F'
         TM    PRMTXT,X'80'           negative number ?
         BZ    NFN                    br if not
         MVI   OUTOPER+2,C'-'         else add - sign
         LA    R4,1(R4)               and increment output pointer
NFN      L     R2,PRMTXT              get constant
         LPR   R2,R2                  make it positive
         CVD   R2,DWFP2               make decimal
         UNPK  DWD2-2(10),DWFP2+2(6)  make it ebcdic
         OI    DWD2+7,C'0'            make sign printable
         LA    R2,DWD2-2              get @ start of output area
         LA    R3,9                   get max len - 1
FZL      CLI   0(R2),C'0'             )
         BNE   NFZ                    ) skip over
         LA    R2,1(R2)               ) leading zeroes
         BCT   R3,FZL                 )
NFZ      EX    R3,MVFZ2               move number
         AR    R4,R3                  calc @ end of number
         MVI   OUTLINE+3,X'7D'        add final '
         LEAVE ,                      return to caller        CFK*NOV03
MVFZ2    MVC   OUTOPER+2(1),0(R2)     EXecuted instruction
         DC    C'IMM='
DWFP2    DC    D'0'                   CVD work area
FQ       DC    X'C67D'                F'
         DC    F'0'                   extension of next field
DWD2     DC    D'0'                   ebcdic work area
SAVEAREA DS    18F                                            CFK*NOV03
         YREGS ,                      register equates        CFK*DEC00
         END
